// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not


CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a= x, b= false, sel= zx, out= s1);
    Not16(in= s1, out= Ns1);
    Mux16(a= s1, b= Ns1, sel= nx, out= s2);

    Mux16(a= y, b= false, sel= zy, out= s3);
    Not16(in= s3, out= Ns3);
    Mux16(a= s3, b= Ns3, sel= ny, out= s4);

    And16(a= s2, b= s4, out= xandy);
    Add16(a= s2, b= s4, out= xplusy);
    Mux16(a= xandy, b= xplusy, sel= f, out= s5);

    Not16(in= s5, out= s6);
    Mux16(a= s5, b= s6, sel= no, out[0..7]=temp1, out[8..15]=temp2,out=out, out[15]=ng);
    
    Or8Way(in= temp1, out= a1);
    Or8Way(in= temp2, out= a2);
    Or(a= a1, b= a2, out= a3);
    Not(in= a3, out= zr);
    
    
    
}

